# Process this file with autoconf to produce a configure script.
# SRE, Thu Jul 27 13:30:41 2000

AC_INIT(src/cmbuild.c)

echo "   Welcome to INFERNAL... configuring for your system."

AC_SUBST(MDEFS)

# Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_RANLIB

# GNU make check; makefile dependency lines for executables.
# original from John Darrington <j.darrington@elvis.murdoch.edu.au> 
# w/ heavy modifications from me.
# We need this because GNU make and SYSV make use different systems
# specifying variables for dependencies: $$@ in sysv, %: %.o in GNU.
# Would love to hear a better way of doing this.
# 
AC_DEFUN(CHECK_GNU_MAKE,[ 
  AC_MSG_CHECKING(whether your make is GNU make)
  foundGNUmake='nope.' ;
  EXEC_DEPENDENCY=[\$\$\@.o] ;
  if ( make --version nothing 2> /dev/null | grep GNU > /dev/null ) ;  then
     foundGNUmake='yes, it is.' ;
     EXEC_DEPENDENCY='%: %.o' ;
  fi
  AC_MSG_RESULT($foundGNUmake)
  AC_SUBST(EXEC_DEPENDENCY)
])
CHECK_GNU_MAKE

dnl INFERNAL can optionally support LFS, the Large File Summit
dnl See: http://ftp.sas.com/standards/large.file/x_open.20Mar96.html
dnl
AC_ARG_ENABLE(lfs, 
[  --enable-lfs         enable LFS, Large File Support],
[case $enable_lfs in
   yes) echo 'Configuring optional LFS, large file support'
        CFLAGS="${CFLAGS} -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE -D_FILE_OFFSET_BITS=64" 
        ;;
   no)  ;;
    *)  echo "Ignoring unknown argument to --enable-lfs: $enable_lfs"
        ;;
esac])


# Checks for system libraries
AC_CHECK_LIB(m, sqrt, LIBS="${LIBS} -lm")
#AC_CHECK_LIB(m, sqrt, LIBS="${LIBS} -lccmalloc")
#AC_CHECK_LIB(m, sqrt, LIBS="${LIBS} -ldl")

# Configure the squid library
#
AC_CONFIG_SUBDIRS(squid)

# Write out Makefiles
echo "   Configuration complete. Writing Makefiles and such..."
AC_OUTPUT(Makefile src/Makefile squid/Makefile)
